equal_prob

<syntax>         ::= <rule> | <rule> <syntax>;
<rule>           ::= <opt-whitespace> "<" <rule-name> ">" <opt-whitespace> "::=" <opt-whitespace> <expression> <line-end>;
<opt-whitespace> ::= " " <opt-whitespace> | "";
<expression>     ::= <list> | <list> <opt-whitespace> "|" <opt-whitespace> <expression>;
<line-end>       ::= <opt-whitespace> <EOL> | <line-end> <line-end>;
<list>           ::= <term> | <term> <opt-whitespace> <list>;
<term>           ::= <literal> | "<" <rule-name> ">";
<literal>        ::= '"' <text1> '"' | "'" <text2> "'";
<text1>          ::= "" | <character1> <text1>;
<text2>          ::= "" | <character2> <text2>;
<character>      ::= <letter> | <digit> | <symbol>;
<letter>         ::= "A" - "Z" | "a" - "z";
<digit>          ::= "0" - "9";
<symbol>         ::= "|" | " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | ">" | "=" | "<" | "?" | "@" | "[" | "\\" | "]" | "^" | "_" | "`" | "{" | "}" | "~";
<character1>     ::= <character> | "'";
<character2>     ::= <character> | '"';
<rule-name>      ::= <letter> | <rule-name> <rule-char>;
<rule-char>      ::= <letter> | <digit> | "-";